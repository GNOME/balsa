dnl ######################################################################################
dnl Startup
dnl ######################################################################################

AC_INIT(src/main.c)
AM_CONFIG_HEADER(config.h)

dnl ######################################################################################
dnl Versioning
dnl   BALSA_VERSION is what we use, except when we want...
dnl   BALSA_SHORT_VERSION, which doesn't have the release appended. For RPM, which
dnl                        keeps the release as a separate field
dnl ######################################################################################

BALSA_MAJOR=0
BALSA_REVISION=8
BALSA_PATCHLEVEL=0
BALSA_RELEASE="pre1"

dnl It's okay to set BALSA_RELEASE= ; this handles it

if test x"$BALSA_RELEASE" = x ; then
	brsffx=
else
	brsffx="-${BALSA_RELEASE}"
fi

BALSA_SHORT_VERSION="$BALSA_MAJOR.$BALSA_REVISION.${BALSA_PATCHLEVEL}"
BALSA_VERSION="$BALSA_MAJOR.$BALSA_REVISION.${BALSA_PATCHLEVEL}${brsffx}"
VERSION="$BALSA_VERSION"

dnl ######################################################################################
dnl Configure libmutt, later
dnl ######################################################################################

AC_CONFIG_SUBDIRS(libmutt)

dnl ######################################################################################
dnl Now that we have versioning, checks and stuff
dnl ######################################################################################

AM_INIT_AUTOMAKE(balsa, [$VERSION])

AM_MAINTAINER_MODE
AM_ACLOCAL_INCLUDE(macros)
AC_ISC_POSIX

AM_PATH_GLIB([1.2.0],[],[
	AC_MSG_ERROR([You need GLib installed -- GNOME requires this as well.])
],gthread)

GNOME_INIT

AC_PROG_CC
AC_STDC_HEADERS
AC_ARG_PROGRAM
AM_PROG_LEX
AC_PROG_YACC
AM_PROG_LIBTOOL

dnl this should come after `AC_PROG_CC'
GNOME_X_CHECKS

dnl ######################################################################################
dnl All the linguas, all the time.
dnl ######################################################################################

ALL_LINGUAS="ca da de el es et fi fr ga it ja ko lt nl no pl pt_BR ru sv tr uk zh_CN"
LINGUAS="$ALL_LINGUAS"
AM_GNU_GETTEXT

dnl ######################################################################################
dnl Substitutions, defines, part I
dnl ######################################################################################

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

dnl No choice about this
gnome_cv_use_gnome=yes

AC_DEFINE_UNQUOTED(BALSA_MAJOR, $BALSA_MAJOR)
AC_DEFINE_UNQUOTED(BALSA_REVISION, $BALSA_REVISION)
AC_DEFINE_UNQUOTED(BALSA_PATCHLEVEL, $BALSA_PATCHLEVEL)
AC_DEFINE_UNQUOTED(BALSA_VERSION, "$BALSA_VERSION")

dnl ######################################################################################
dnl More checks
dnl ######################################################################################

AC_CHECK_LIB(crypt, crypt)

dnl these may be used later.
dnl AC_CHECK_LIB(des, des_encrypt)
dnl AC_CHECK_LIB(krb, krb_sendauth)
dnl AC_CHECK_LIB(acap, acap_OpenConnection)

AM_HDR_SIGACTION
AM_HDR_SIGSET

dnl ######################################################################################
dnl ORBit support
dnl Use 'failure' instead of 'dontfail' to make us bomb if no orbit. Probably should
dnl   use 'failure'.
dnl ######################################################################################

GNOME_ORBIT_HOOK([],dontfail)
AM_CONDITIONAL(ORBIT_INSTALLED, test -n "$ORBIT_LIBS")

dnl ######################################################################################
dnl More substitutions
dnl ######################################################################################

AC_SUBST(BALSA_MAJOR)
AC_SUBST(BALSA_REVISION)
AC_SUBST(BALSA_PATCHLEVEL)
AC_SUBST(BALSA_RELEASE)
AC_SUBST(BALSA_SHORT_VERSION)
AC_SUBST(BALSA_VERSION)

AC_SUBST(LIBOBJS)
AC_SUBST(LIBESD_LIB)

dnl ######################################################################################
dnl Check for CVS nature, for warnings + extra program checks
dnl ######################################################################################

if test -d "${srcdir}/CVS" ; then
	BALSA_FROM_CVS=yes
else
	BALSA_FROM_CVS=no
fi

AC_MSG_CHECKING([for CVS information])
AC_MSG_RESULT($BALSA_FROM_CVS)

dnl ######################################################################################
dnl Enable all? (=broken stuff)
dnl ######################################################################################

AC_ARG_ENABLE(all, [  --enable-all            Enable incomplete features [default=no]],[
	use_all=$enableval
],[
	use_all=no
])

AC_MSG_CHECKING([whether to build unfinished features])
if test x"$use_all" = xyes ; then
	AC_MSG_RESULT([yes])
	AC_DEFINE(BALSA_SHOW_ALL)
else
	AC_MSG_RESULT([no])
fi

dnl ######################################################################################
dnl Enable information in the mailbox list?
dnl ######################################################################################

AC_ARG_ENABLE(info, [  --enable-info           Enable mailbox info [default=yes]],[
	use_info=$enableval
],[
	use_info=yes
])

AC_MSG_CHECKING([whether to use 'mailbox info'])
if test x"$use_info" = xyes ; then
	AC_MSG_RESULT([yes])
	AC_DEFINE(BALSA_SHOW_INFO)
else
	AC_MSG_RESULT([no])
fi

dnl ######################################################################################
dnl Use threading?
dnl ######################################################################################

AC_ARG_ENABLE(threads, [  --enable-threads        Use threading for mail retrieval (unstable) [default=no]],[
	use_threads=$enableval
],[
	use_threads=no
])

AC_MSG_CHECKING([whether to use threads])
if test x"$use_threads" = xyes ; then
	AC_MSG_RESULT([yes])
	if test ! -z `$GLIB_CONFIG --help 2>&1 |grep 'gthread'` ; then
		THREAD_CFLAGS=`$GLIB_CONFIG --cflags gthread`
		THREAD_LIBS=`$GLIB_CONFIG --libs gthread`
		CFLAGS="$CFLAGS $THREAD_CFLAGS"
		LIBS="$LIBS $THREAD_LIBS"
		AC_DEFINE(BALSA_USE_THREADS,1)
	else
		AC_MSG_ERROR([Balsa requires GThread from GLib to use threading.])
	fi
else
	AC_MSG_RESULT([no])
fi

dnl ######################################################################################
dnl Install into gnome locations? (may require priveliges that we don't have)
dnl ######################################################################################

AC_ARG_ENABLE([system-install], [  --disable-system-install Don't install some files into Gnome's prefix],[
	use_system_install=$enableval
],[
	if test x"$BALSA_DISTCHECK_HACK" = xyes ; then
		use_system_install=no
	else
		use_system_install=yes
	fi
])

AC_MSG_CHECKING([whether to install into Gnome's prefix])
if test x"$use_system_install" = xyes ; then
	AC_MSG_RESULT([yes])
	gnomedatadir=`${GNOME_CONFIG} --datadir`
	gnomeconfdir=`${GNOME_CONFIG} --sysconfdir`
else
	AC_MSG_RESULT([no])
	gnomedatadir=`eval "echo ${datadir}"`
	gnomeconfdir=`eval "echo ${sysconfdir}"`
fi

AC_SUBST(gnomedatadir)
AC_SUBST(gnomeconfdir)

dnl ######################################################################################
dnl Remember our prefixes
dnl   The way prefix et al are defined makes us jump through some hoops.
dnl ######################################################################################

dnl datadir='${prefix}/share', so we must eval it or something. This works
BALSA_STD_PREFIX=`eval "echo ${prefix}"`
BALSA_DATA_PREFIX=`eval "echo ${datadir}"`
GNOME_STD_PREFIX=`${GNOME_CONFIG} --prefix`
GNOME_LIB_PREFIX=`${GNOME_CONFIG} --libdir`
GNOME_DATA_PREFIX=`${GNOME_CONFIG} --datadir`

dnl Consolidate them!
BALSA_COMMON_PREFIXES=`cat <<EOF |sort |uniq
"${BALSA_STD_PREFIX}",
"${BALSA_DATA_PREFIX}",
"${GNOME_STD_PREFIX}",
"${GNOME_DATA_PREFIX}",
"${GNOME_LIB_PREFIX}",
EOF
`

dnl Better way to do this?
AC_DEFINE_UNQUOTED( BALSA_STD_PREFIX, "$BALSA_STD_PREFIX" )
AC_DEFINE_UNQUOTED( BALSA_DATA_PREFIX, "$BALSA_DATA_PREFIX" )
AC_DEFINE_UNQUOTED( GNOME_STD_PREFIX, "$GNOME_STD_PREFIX" )
AC_DEFINE_UNQUOTED( GNOME_LIB_PREFIX, "$GNOME_LIB_PREFIX" )
AC_DEFINE_UNQUOTED( GNOME_DATA_PREFIX, "$GNOME_DATA_PREFIX" )
AC_DEFINE_UNQUOTED( BALSA_COMMON_PREFIXES, `echo $BALSA_COMMON_PREFIXES` ) dnl bleah

dnl ######################################################################################
dnl This is for the RPM spec, where we patch the @-@ thingies but we don't want
dnl them substituted in the .infile->file conversion. It's hard to explain, look at
dnl balsa.spec.in and think about it.
dnl ######################################################################################

at_prefix="@prefix@"
at_srcdir="@srcdir@"
at_DATADIRNAME="@DATADIRNAME@"
at_exec_prefix="@exec_prefix@"

AC_SUBST(at_prefix)
AC_SUBST(at_srcdir)
AC_SUBST(at_DATADIRNAME)
AC_SUBST(at_exec_prefix)

dnl ######################################################################################
dnl Offset tool
dnl ######################################################################################

BALSA_OFFSET_TOOL([src/Makefile.offset],[src/cfg-balsa.c],
[	
	CFLAGS="${CFLAGS} ${GNORBA_CFLAGS}"
	CFLAGS="\${CFLAGS} -I\${top_builddir} -I\${top_builddir}/src -I\${top_builddir}/libmutt"
	CFLAGS="\${CFLAGS} -I\${top_srcdir} -I\${top_srcdir}/libmutt -I\${top_srcdir}/libbalsa"
	CFLAGS="\${CFLAGS} -I\${top_srcdir}/src"
])

dnl ######################################################################################
dnl Things needed by CVS user
dnl ######################################################################################

if test x"$BALSA_FROM_CVS" = xyes ; then
	AC_CHECK_PROG(HAS_GOB, gob, yes, no)

	if test x"$HAS_GOB" = xno ; then
		AC_MSG_ERROR([As a CVS user, you need to get gob to build Balsa. It is available from Gnome CVS in the module 'gob'])
	fi
fi

dnl ######################################################################################
dnl Create files.
dnl ######################################################################################

AC_OUTPUT([
Makefile
balsa.1
balsa.spec
po/Makefile.in
macros/Makefile
macros/offset-tool.sh
intl/Makefile
sounds/Makefile
images/Makefile
help/Makefile
help/C/Makefile
libbalsa/Makefile
libinit_balsa/Makefile
idl/Makefile
src/Makefile
])

dnl ######################################################################################
dnl Summary
dnl ######################################################################################

echo ""
echo "================ Final configuration ==================="
echo "    Installing into prefix: $prefix"
echo "      Using multithreading: $use_threads"
echo " Using incomplete features: $use_all"
echo "        Using mailbox info: $use_info"
echo "Installing some files into  "
echo "            Gnome's prefix: $use_system_install (may require root privileges)"
echo ""

if test x"$BALSA_FROM_CVS" = xyes ; then
	echo "============================== NOTICE ================================"
	echo "                 You are using Balsa from CVS source."
	echo "  The program is likely to be unstable, contain incomplete features,"
	echo "or just plain not work. Use it at your own risk. You have been warned."
	echo "======================================================================"
	echo ""
fi

dnl -*- mode: m4 -*-
dnl follow section 5.3 of Autoconf, Automake book.
dnl
dnl 1. Boilerplate.
dnl
AC_PREREQ(2.53)
AC_INIT(src/balsa-app.h)
dnl ###########################################################################
dnl Boilerplace and versioning
dnl ###########################################################################

BALSA_MAJOR=2
BALSA_REVISION=4
BALSA_PATCHLEVEL=7
BALSA_VERSION="$BALSA_MAJOR.$BALSA_REVISION.${BALSA_PATCHLEVEL}"

AC_SUBST(BALSA_MAJOR)
AC_SUBST(BALSA_REVISION)
AC_SUBST(BALSA_PATCHLEVEL)
AC_SUBST(BALSA_VERSION)

AM_INIT_AUTOMAKE(balsa, "$BALSA_MAJOR.$BALSA_REVISION.${BALSA_PATCHLEVEL}")
# Automake-1.11 option for less verbose output
# configure --disable-silent-rules to restore verbose
m4_ifdef([AM_SILENT_RULES],[AM_SILENT_RULES([yes])])

AC_CONFIG_SRCDIR([src/main.c])
AC_CONFIG_MACRO_DIR([m4])
AC_CONFIG_HEADERS([config.h])

AC_DEFINE_UNQUOTED(BALSA_MAJOR, $BALSA_MAJOR,
                   [Define to the Balsa major version])
AC_DEFINE_UNQUOTED(BALSA_REVISION, $BALSA_REVISION,
                   [Define to the Balsa revision])
AC_DEFINE_UNQUOTED(BALSA_PATCHLEVEL, $BALSA_PATCHLEVEL,
                   [Define to the Balsa patch level])
AC_DEFINE_UNQUOTED(BALSA_VERSION, "$BALSA_VERSION",
                   [Define to the Balsa version])

dnl #####################################################################
dnl 2. Options
dnl #####################################################################
AC_ARG_WITH([gnome],
        AC_HELP_STRING([--with-gnome],
                [Use GNOME libraries (default=yes)]),[
	with_gnome=$withval
],[
	with_gnome=yes
])

dnl ###########################################################################
dnl Configure locking
dnl ###########################################################################

AC_ARG_ENABLE(flock,
        AC_HELP_STRING([--enable-flock],
                       [Do NOT use flock() to lock files (default=no)]),
	[if test x$enableval = xno; then balsa_cv_flock=no; fi])

if test x$balsa_cv_flock = xyes; then
	AC_DEFINE(USE_FLOCK,1,[Define if flock locking should be used.])
fi

balsa_cv_fcntl=yes
AC_ARG_ENABLE(fcntl,
        AC_HELP_STRING([--disable-fcntl],
                       [Use fcntl() to lock files (default=yes)]),
	[if test x$enableval = no; then balsa_cv_fcntl=no; fi])
if test x$balsa_cv_fcntl = xyes; then
	AC_DEFINE(USE_FCNTL,1,[Define if fcntl locking should be used.])
fi

AC_ARG_ENABLE(threads,
        AC_HELP_STRING([--enable-threads],
                       [Use threading for mail retrieval (default=yes)]),[
	use_threads=$enableval
],[
	use_threads=yes
])

if test x"$use_threads" = xyes; then 
        AC_DEFINE(BALSA_USE_THREADS, 1,[Defined when balsa is to use threads.])
fi


AC_ARG_WITH([gpgme],
   AC_HELP_STRING([--with-gpgme=gpgme-config],
                  [build with gpgme/GnuPG support (default=no, path to gpgme-config optional)]),
	          [ gpgmecfg=$withval ], [ gpgmecfg=no ])
AC_ARG_ENABLE([smime],
   AC_HELP_STRING([--enable-smime],
                  [include S/MIME support (needs gpgme and gpgsm, experimental for gpg < 2.0.4)]),
	          [ have_smime=$enableval ], [ have_smime=check ])
AC_ARG_WITH([gpg-app],
   AC_HELP_STRING([--with-gpg-app=PATH],
                  [use PATH as GnuPG application (default=gpg2 if >= 2.0.4, otherwise gpg 1.x)]),
	          [ gpgapp=$withval ], [ gpgapp=no ])



AC_ARG_WITH(canberra,
   AC_HELP_STRING([--with-canberra],
                  [Use libcanberra-gtk2 for filter sounds (default=no)]),
                  [with_canberra=$withval],[with_canberra=no])

AC_ARG_WITH(compface,
   AC_HELP_STRING([--with-compface],
                  [Enable Compface (default=no)]),
                  [with_compface=$withval],[with_compface=no])

AC_ARG_WITH(esmtp,
   AC_HELP_STRING([--with-esmtp=DIR],
                  [Enable libESMTP (default=yes)]),
                  [ with_esmtp=$withval ],[ with_esmtp=yes ])

AC_ARG_ENABLE([gregex], 
   AC_HELP_STRING([--enable-gregex],
                  [Use GRegex regular expressions if available (default=no)]),
                  [ check_gregex=$enableval],[ check_gregex=no ])

AC_ARG_WITH(gss,
   AC_HELP_STRING([--with-gss],
                  [Enable GSS (default=no)]),
                  [with_gss=$withval],[with_gss=no])

AC_ARG_WITH(html-widget,
            AC_HELP_STRING([--with-html-widget=(no|gtkhtml2|gtkhtml3|webkit)],
                           [select the HTML renderer (default gtkhtml3)]),
            [use_html_widget=$withval],
            [use_html_widget=gtkhtml3])

AC_ARG_WITH([gtksourceview],
   AC_HELP_STRING([--with-gtksourceview],
                  [Use GtkSourceView if available (default=no, specify version 1 or 2, yes=2)]),
		  [with_gtksourceview=$withval],[with_gtksourceview=no])

AC_ARG_WITH([gtkspell],
   AC_HELP_STRING([--with-gtkspell],
                  [Use GtkSpell/enchant if available (default=no)]),
		  [with_gtkspell=$withval],[with_gtkspell=no])

AC_ARG_WITH(iconv,      
   AC_HELP_STRING([--with-iconv=DIR],
                  [Where libiconv is installed]),
		  [if test "$with_iconv" != yes -a "$with_iconv" != no ; then
                   CPPFLAGS="-I${with_iconv}/include $CPPFLAGS"
                   LDFLAGS="-liconv -L${with_iconv}/lib $LDFLAGS"
		   fi])

AC_ARG_WITH([ldap],
   AC_HELP_STRING([--with-ldap=DIR],
                  [Use OpenLDAP if available (default=no)]),
		  [with_ldap=$withval],[with_ldap=no])

AC_ARG_WITH([libnotify],
   AC_HELP_STRING([--with-libnotify],
                  [Use libnotify if available (default=yes)]),
		  [with_libnotify=$withval],[with_libnotify=yes])

AC_ARG_WITH([macosx-desktop],
   AC_HELP_STRING([--with-macosx-desktop],
	          [Use Mac OS X Desktop Integration (default=no, extremely experimental)]),
		  [use_igemacint=$withval],[use_igemacint=no])

AC_ARG_WITH(nm,
   AC_HELP_STRING([--with-nm],
                  [Enable NetworkManager (default=yes)]),
                  [with_nm=$withval],[with_nm=yes])

AC_ARG_ENABLE([pcre], 
   AC_HELP_STRING([--enable-pcre],
                  [Use Perl-compatible regular expressions if available (default=no)]),
                  [ check_pcre=$enableval],[ check_pcre=no ])


AC_ARG_WITH([rubrica],
   AC_HELP_STRING([--with-rubrica],
                  [add Rubrica2 address book support (needs libxml2, default=no)]),
		  [with_rubrica=$withval],[with_rubrica=no])

AC_ARG_WITH([sqlite],
   AC_HELP_STRING([--with-sqlite=DIR],
                  [Use SQLite for GPE address books (default=no)]),
                  [with_sqlite=$withval],[with_sqlite=no])

AC_ARG_WITH(ssl,
   AC_HELP_STRING([--with-ssl],
                  [Enable SSL (default=no)]),
                  [ with_ssl=$withval ],[ with_ssl=no ])

AC_ARG_WITH(unique,
   AC_HELP_STRING([--with-unique],
                  [Use libUnique instead of BonoboActivation (default=no)]),
                  [with_unique=$withval],[with_unique=no])

AC_ARG_ENABLE(more-warnings,
   AC_HELP_STRING([--enable-more-warnings],
                  [Enable maximum compiler warnings (default=yes)]),
                  [set_more_warnings="$enableval"], [set_more_warnings=yes])


dnl ##########################################################################
dnl Enable touchscreen optimized UI?
dnl ##########################################################################

AC_ARG_ENABLE(touch-ui,
              AC_HELP_STRING([--enable-touch-ui],
                             [Enable Touchscreen UI mode (default=no)]),
              [enable_touch_ui="$enableval"],[enable_touch_ui=no])

dnl #####################################################################
dnl 3. Programs: compilers and their options.
dnl #####################################################################
AC_PROG_CC
m4_ifdef([AM_PROG_LIBTOOL],[AM_PROG_LIBTOOL],[AC_PROG_RANLIB])
AC_PROG_LN_S

# pkg-config
#
AC_PATH_PROG(PKG_CONFIG, pkg-config, no)

# scrollkeeper.
#

AC_PATH_PROG(gdtool, gnome-doc-tool, no)
if test "$gdtool" = no; then
with_g_d_u=no
else
   AC_MSG_CHECKING([whether we have gnome-doc-utils])
   if $PKG_CONFIG --exists gnome-doc-utils; then
       with_g_d_u=yes
   else
       with_g_d_u=no
   fi
   AC_MSG_RESULT($with_g_d_u)
fi


AM_CONDITIONAL([BUILD_WITH_G_D_U], [test $with_g_d_u = "yes"])

if test $with_g_d_u = yes ; then
GNOME_DOC_INIT
else
AM_CONDITIONAL([HAVE_GNOME_DOC_UTILS],[false])
AM_CONDITIONAL([ENABLE_SK],[false])
fi

# glib-genmarshal
#
AC_SUBST(GLIB_GENMARSHAL)
GLIB_GENMARSHAL=`pkg-config --variable=glib_genmarshal glib-2.0`

# gettext
#
ALL_LINGUAS="am ar az be bg ca cs da de dz el en_CA en_GB es et eu fi fr ga he hi hr hu id it ja ko lt lv mk ml ms nb ne nl nn oc pa pl pt pt_BR ro ru rw sk sl sq sr sr@Latn sv tr uk vi wa zh_CN zh_HK zh_TW"
GETTEXT_PACKAGE=balsa
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE",
        [The gettext package name.])

AC_PROG_INTLTOOL
AM_GLIB_GNU_GETTEXT
AC_CHECK_HEADER([langinfo.h],
                [AC_DEFINE([HAVE_LANGINFO],1,[Defined for langinfo.])],
                [])

# sendmail
#
if test x"$with_esmtp" = xno; then
AC_PATH_PROG(SENDMAIL, sendmail, no, $PATH:/usr/sbin:/usr/lib)
AC_DEFINE_UNQUOTED(SENDMAIL, "$ac_cv_path_SENDMAIL",
		   [Path to sendmail program.])
fi

dnl #####################################################################
dnl GNOME libs
dnl #####################################################################
if test x$with_gnome != xno; then
   gnome_extras="libgnome-2.0 libgnomeui-2.0 libbonobo-2.0 gconf-2.0"
   AC_MSG_CHECKING([whether we have gnome-keyring])
   if $PKG_CONFIG --atleast-version=1.0 gnome-keyring-1; then
      gnome_extras="$gnome_extras gnome-keyring-1"
      AC_DEFINE(HAVE_GNOME_KEYRING,1,[Defined when gnome-keyring is there.])
      # Work around http://bugzilla.gnome.org/show_bug.cgi?id=556530
      if $PKG_CONFIG --atleast-version=2.24.1 gnome-keyring-1; then
         AC_DEFINE(HAVE_GNOME_KEYRING_24,1,
                   [Defined with gnome-keyring-2.24 or newer.])
      fi
      with_gnome_keyring=yes
   else
      with_gnome_keyring=no
   fi
   AC_MSG_RESULT($with_gnome_keyring)
   AC_DEFINE(HAVE_GNOME,1,[Defined when gnome libs are available.])
else
   gnome_extras=""
fi
AM_CONDITIONAL([BUILD_WITH_GNOME], [test $with_gnome = "yes"])

dnl #####################################################################
dnl 4. Libraries.
dnl #####################################################################

PKG_CHECK_MODULES(BALSA, [
glib-2.0 >= 2.16.0
gtk+-2.0 >= 2.14.0
gmime-2.4
gio-2.0
gnome-icon-theme
$gnome_extras
])

PKG_CHECK_MODULES(BALSA_AB, [
   glib-2.0 >= 2.6.0
   gtk+-2.0 >= 2.6.0
   gmime-2.4
   $gnome_extras
])

AC_MSG_CHECKING(whether res_init is available)
AC_LINK_IFELSE(AC_LANG_PROGRAM([#include <resolv.h>
                               ],
                               [res_init();]),
               have_res_init=yes,
               have_res_init=no)
if test $have_res_init = no ; then
    save_LIBS=$LIBS
    LIBS="$LIBS -lresolv"
    AC_LINK_IFELSE(AC_LANG_PROGRAM([#include <resolv.h>
                                   ],
                                   [res_init();]),
                   have_res_init=yes)
fi
AC_MSG_RESULT([$have_res_init])
if test $have_res_init = no ; then
    LIBS=$save_LIBS
else
    AC_DEFINE(HAVE_RES_INIT,1,
              [Defined when res_init is available.])
fi

dnl ##########################################################################
dnl Check versions
dnl ##########################################################################

# CORBA interface.
#
                                                                        
ORBIT_IDL="`$PKG_CONFIG --variable=orbit_idl ORBit-2.0`"
AC_SUBST(ORBIT_IDL)

LIBBONOBO_IDL="`$PKG_CONFIG --variable=idldir libbonobo-2.0`"
AC_SUBST(LIBBONOBO_IDL)
 
BONOBO_ACTIVATION_IDL="`$PKG_CONFIG --variable=idldir bonobo-activation-2.0`"
AC_SUBST(BONOBO_ACTIVATION_IDL)

AC_SUBST(BALSA_CFLAGS)
AC_SUBST(BALSA_LIBS)
AC_SUBST(BALSA_AB_LIBS)
AC_SUBST(BALSA_DEFS)

# HTML widget
#
AC_MSG_CHECKING(whether to use an HTML widget)
case "$use_html_widget" in
    gtkhtml2)
        AC_MSG_RESULT([$use_html_widget])
        PKG_CHECK_MODULES(HTML, [ libgtkhtml-2.0 ])
        AC_DEFINE(HAVE_GTKHTML2,1,[Defined when GtkHtml-2 can be used.])
    ;;
    gtkhtml3)
        AC_MSG_RESULT([$use_html_widget])
        PKG_CHECK_MODULES(HTML, [ libgtkhtml-3.14 ])
        AC_DEFINE(HAVE_GTKHTML3,1,[Defined when GtkHtml-3 can be used.])
    ;;
    webkit)
        AC_MSG_RESULT([$use_html_widget])
        if ! $PKG_CONFIG --atleast-version=1.1.14 webkit-1.0; then
            AC_MSG_ERROR([WebKitGTK+ version 1.1.14 or newer is required.])
        fi
        if test x"$use_threads" = xno ; then
            AC_MSG_ERROR([WebKit cannot be used with threads disabled.])
        fi
        PKG_CHECK_MODULES(HTML, [webkit-1.0])
        AC_DEFINE(HAVE_WEBKIT,1,[Defined when WebKit can be used.])
    ;;
    no)
        AC_MSG_RESULT([none])
    ;;
    *) AC_MSG_ERROR([bad option $use_html_widget]) ;;
esac

if test x"$use_html_widget" != xno ; then
    AC_DEFINE(HAVE_HTML_WIDGET,1,[Defined when any HTML widget can be used.])
    BALSA_CFLAGS="$BALSA_CFLAGS $HTML_CFLAGS"
    BALSA_LIBS="$BALSA_LIBS $HTML_LIBS"
fi


# GPGME detection.
# We don't use the std macro AM_PATH_GPGME here to avoid dependencies.

AC_MSG_CHECKING(whether to build with gpgme/GnuPG support)
if test x"$gpgmecfg" = xyes ; then
	AC_MSG_RESULT(yes)
	AC_PATH_PROG(gpgmecfg, gpgme-config, no)
	if test "$gpgmecfg" = no; then
	AC_MSG_ERROR([GPGme build requested but gpgme-config program not found.])
        fi
fi
if test x"$gpgmecfg" != xno ; then
	AC_MSG_CHECKING([gpgme library version])
	gpgmever=`$gpgmecfg --version`
	AC_MSG_RESULT($gpgmever)
        gpgme_ve=`echo $gpgmever|sed 's/\([[0-9]]*\).\([[0-9]]*\).\([[0-9]]*\)/\1/'`
        gpgme_ma=`echo $gpgmever|sed 's/\([[0-9]]*\).\([[0-9]]*\).\([[0-9]]*\)/\2/'`
        gpgme_mi=`echo $gpgmever|sed 's/\([[0-9]]*\).\([[0-9]]*\).\([[0-9]]*\)/\3/'`
	if test $gpgme_ve -eq 0 ; then
		if test $gpgme_ma -lt 9 ; then
			gpgmecfg=no
			have_smime=no
		fi
	fi
	if test x"$gpgmecfg" != xno ; then
		if test x"$use_threads" = xyes; then
			BALSA_LIBS="$BALSA_LIBS `$gpgmecfg --thread=pthread --libs`"
			if test x"$?" != x0 ; then
				AC_MSG_ERROR([*** You enabled gpgme and pthread support, but your gpgme installation does not support threads.])
			fi
		else
			BALSA_LIBS="$BALSA_LIBS `$gpgmecfg --libs`"
		fi
		AC_DEFINE(HAVE_GPGME,1,[Defined when gpgme is available.])
		BALSA_CFLAGS="$BALSA_CFLAGS `$gpgmecfg --cflags`"
		gpgmecfg=yes
	else
		AC_MSG_WARN([sorry, you need at least gpgme version 0.9.0])
	fi
else
	have_smime=no
fi
AM_CONDITIONAL([BUILD_WITH_GPGME], [test $gpgmecfg = "yes"])

# check the GnuPG engine application version
# note: gpg2 interacts fine with gpgme only since version 2.0.4
AC_DEFUN([AC_CHECK_GPG_VER],[
	if ! test -x $1 ; then
		AC_MSG_ERROR([$1 is not executable])
	fi
	AC_MSG_CHECKING([$1 version])
	gpgver=`$1 --version | sed -e 's/.* //' -e q`
	AC_MSG_RESULT($gpgver)
        gpg_ve=`echo $gpgver|sed 's/\([[0-9]]*\).\([[0-9]]*\).\([[0-9]]*\)/\1/'`
        gpg_ma=`echo $gpgver|sed 's/\([[0-9]]*\).\([[0-9]]*\).\([[0-9]]*\)/\2/'`
        gpg_mi=`echo $gpgver|sed 's/\([[0-9]]*\).\([[0-9]]*\).\([[0-9]]*\)/\3/'`
	HAVE_GPG2=no
	if test \( $gpg_ve -eq 2 \) -a \( $gpg_ma -eq 0 \) -a \( $gpg_mi -lt 4 \) ; then
    		ifelse([$3], , :, [$3])
	else
		if test $gpg_ve -eq 2 ; then
			HAVE_GPG2=yes
		fi
    		ifelse([$2], , :, [$2])
	fi])

# find a suitable GnuPG engine if gpgme is enabled
if test x"$gpgmecfg" != xno ; then
	if test x"$gpgapp" != xno ; then
		AC_CHECK_GPG_VER($gpgapp, , 
				 AC_MSG_ERROR([gpg2 < 2.0.4 does not communicate properly with gpgme]))
	else
		AC_PATH_PROG(gpg2, gpg2, [no])
		if test x"$gpg2" != xno ; then
			AC_CHECK_GPG_VER($gpg2, [gpgapp=$gpg2],
					 AC_MSG_WARN([gpg2 < 2.0.4 does not communicate properly with gpgme]))
		fi
		if test x"$gpgapp" = xno ; then
			AC_PATH_PROG(gpg, gpg, [no])
			if test x"$gpg" != xno ; then
				AC_CHECK_GPG_VER($gpg, [gpgapp=$gpg],
					 	AC_MSG_WARN([gpg2 < 2.0.4 does not communicate properly with gpgme]))
			fi
		fi
		if test x"$gpgapp" = xno ; then
			AC_PATH_PROG(gpg1, gpg1, [no])
			if test x"$gpg1" != xno ; then
				AC_CHECK_GPG_VER($gpg1, [gpgapp=$gpg1],
					 	AC_MSG_WARN([gpg2 < 2.0.4 does not communicate properly with gpgme]))
			fi
		fi
	fi
	if test x"$gpgapp" != xno ; then
		AC_DEFINE(HAVE_GPG,1,[Defined when gpg is available.])
		AC_DEFINE_UNQUOTED(GPG_PATH,["$gpgapp"],[Path of gpg.])
	else
		AC_MSG_WARN([cannot find a suitable gpg application])
		AC_MSG_WARN([please consider using --with-gpg-app to specify it])
	fi
else
	AC_MSG_RESULT([no])
fi

# s/mime is mature for gpg >= 2.0.4
AC_MSG_CHECKING(whether to include S/MIME support)
if test \( x"$have_smime" = xyes \) -o \( \( x"$HAVE_GPG2" = xyes \) -a \( x"$have_smime" = xcheck \) \) ; then
	AC_MSG_RESULT([yes])
	have_smime=yes
	AC_DEFINE(HAVE_SMIME,1,[Defined when supporting S/MIME])
else
	AC_MSG_RESULT([no])
fi
AM_CONDITIONAL([BUILD_WITH_SMIME], [test $have_smime = "yes"])


# OpenLDAP configuration.
#
AC_MSG_CHECKING(whether to use LDAP)
if test x"$with_ldap" != xno ; then
	AC_MSG_RESULT([yes])
	AC_CHECK_LIB(ldap, ldap_search, AC_DEFINE(ENABLE_LDAP, 1,
                 [Defined when the LDAP support is to be compiled in.])
              LIBS="-lldap -llber -lresolv $LIBS", AC_MSG_ERROR([*** You enabled LDAP but ldap library is not found.]), -llber -lresolv)
        AC_CHECK_LIB(ldap, ldap_start_tls_s,
                     AC_DEFINE(HAVE_LDAP_TLS,1,
                               [Defined when LDAP lib supports TLS]))
    if test "$with_ldap" != yes; then
       LIBS="$LIBS -L$with_ldap/lib"
       CPPFLAGS="$CPPFLAGS -I$with_ldap/include"
    fi
else
	AC_MSG_RESULT([no])
fi

# SQLite configuration.
#
AC_MSG_CHECKING(whether to use SQLite)
if test x$with_sqlite != xno ; then
    AC_MSG_RESULT([yes])
    if $PKG_CONFIG --exists sqlite3 ; then
        PKG_CHECK_MODULES(SQLITE, [sqlite3])
        AC_DEFINE(HAVE_SQLITE3,1,[If defined, use sqlite3 api.])
    else
        PKG_CHECK_MODULES(SQLITE, [sqlite >= 2.8])
    fi
    AC_DEFINE(HAVE_SQLITE,1,[If defined, enable GPE address book support.])
    BALSA_CFLAGS="$BALSA_CFLAGS $SQLITE_CFLAGS"
    BALSA_LIBS="$BALSA_LIBS $SQLITE_LIBS"
    BALSA_AB_LIBS="$BALSA_AB_LIBS $SQLITE_LIBS"
else
    AC_MSG_RESULT([no])
fi

# Rubrica (libxml2) configuration
#
AC_MSG_CHECKING(whether to include Rubrica2 support)
if test x$with_rubrica != xno ; then
    AC_MSG_RESULT([yes])
    PKG_CHECK_MODULES(LIBXML2, [libxml-2.0])
    AC_DEFINE(HAVE_RUBRICA,1,[If defined, enable Rubrica2 address book support.])
    BALSA_CFLAGS="$BALSA_CFLAGS $LIBXML2_CFLAGS"
    BALSA_LIBS="$BALSA_LIBS $LIBXML2_LIBS"
    BALSA_AB_LIBS="$BALSA_AB_LIBS $LIBXML2_LIBS"
else
    AC_MSG_RESULT([no])
fi

# LibESMTP configuration.
#
AC_MSG_CHECKING([whether to build ESMTP support])
if test "x$with_esmtp" != xno  ; then
    AC_MSG_RESULT([yes])
    test "$with_esmtp" != yes && LIBS="$LIBS -L$with_esmtp/lib"
    AC_CHECK_LIB(esmtp, smtp_start_session,BALSA_LIBS="$BALSA_LIBS -lesmtp",
	AC_MSG_ERROR([*** You enabled ESMTP but esmtp library is not found.]),
                 -lpthread)
    AC_CHECK_LIB(esmtp, smtp_option_require_all_recipients, 
                 AC_DEFINE(ENABLE_ESMTP, 1, 
                           [Defined when ESTMP support is to be enabled.]),
	AC_MSG_ERROR([*** You enabled ESMTP but esmtp library doesn't support
	    'require_all_recipients'. Please get a Balsa aware
	    libESMTP from http://balsa.gnome.org/main.html or compile
	    libESMTP with './configure --enable-require-all-recipients'.]),
                 -lpthread)
    AC_CHECK_LIB(esmtp, smtp_starttls_set_password_cb,
	AC_DEFINE(HAVE_SMTP_TLS_CLIENT_CERTIFICATE,1,
	         [Defined when libESMTP has TLS support.]),
                 ,
		 -lpthread)
    test "$with_esmtp" != yes && CPPFLAGS="$CPPFLAGS -I$with_esmtp/include"
else
    AC_MSG_RESULT([no])
fi

# PCRE configuration.
#

AC_MSG_CHECKING(Regular expressions)
if test x"$check_gregex" = xyes ; then
    if $PKG_CONFIG --atleast-version=2.14.0 glib-2.0; then
        AC_MSG_RESULT(GRegex)
        AC_DEFINE(USE_GREGEX,1,[Defined when GRegex is to be used.])
    else
        AC_MSG_ERROR([*** You enabled GRegex but glib is too old. Please
                      upgrade glib to version at least 2.14.0.])
    fi
else
   if   test x"$check_pcre" = xyes ; then
	if pcre-config --libs-posix > /dev/null 2>&1; then
        	vers=`pcre-config --version`
	        case $vers in
                       2.*) have_pcre=no ;;
                       *)   have_pcre=yes ;;
        	esac
	else
        	have_pcre=no
	fi

	if test x"$have_pcre" = xyes ; then
	       	AC_MSG_RESULT(PCRE)
		BALSA_CFLAGS="$BALSA_CFLAGS `pcre-config --cflags-posix`"
                PCRE_LIBS="`pcre-config --libs-posix`"
                AC_SUBST(PCRE_LIBS)
		BALSA_LIBS="$BALSA_LIBS $PCRE_LIBS"
		AC_DEFINE(HAVE_PCRE,1,
                          [Defined when PCRE library is available.])
	else
	        AC_MSG_RESULT(not found)
	fi
   else
	have_pcre=no
	AC_MSG_RESULT(POSIX)
   fi
fi

# OpenSSL configuration.
#

AC_MSG_CHECKING([whether to build SSL support])
if test "x$with_ssl" != xno  ; then
# openssl >=0.9.7 is pkg-config compatible.
   if pkg-config openssl >/dev/null 2>&1; then
      BALSA_LIBS="$BALSA_LIBS `pkg-config --libs openssl`"
      BALSA_CFLAGS="`pkg-config --cflags-only-I openssl` $BALSA_CFLAGS"
      AC_MSG_RESULT(yes)
      AC_DEFINE(USE_SSL,1,[Defined when SSL support is to be compiled.])
      AC_DEFINE(USE_TLS,USE_SSL,[Defined when TLS is to be enabled.])
   else
      if test -d $with_ssl && test -f "$with_ssl/include/openssl/ssl.h" ; then
         ssldir=$with_ssl
      else
           AC_TRY_COMPILE([#include <openssl/ssl.h>], ,
		          ssldir=default,
			  ssldir="")
	   if test "x$ssldir" = x ; then		
              for prefixdir in /usr /usr/local /usr/lib /usr/pkg /var /opt; do
                  for dir in $prefixdir $prefixdir/openssl $prefixdir/ssl; do
                      if test -f "$dir/include/openssl/ssl.h"; then
                         ssldir=$dir
                      fi
                  done
              done
	   fi
       fi
       if test -z "$ssldir"; then
          AC_MSG_ERROR([SSL requested but no SSL headers found.])
       else
          AC_MSG_RESULT(yes)
          BALSA_LIBS="$BALSA_LIBS -lssl -lcrypto"
	  if test "x$ssldir" != xdefault ; then
		BALSA_CFLAGS="-I${ssldir}/include $BALSA_CFLAGS"
	  fi
	  ac_cv_path_openssl=$ssldir
	  AC_DEFINE(USE_SSL,1,[Defined when SSL support is to be compiled.])
          AC_DEFINE(USE_TLS,USE_SSL,[Defined when TLS is to be enabled.])
       fi
   fi
else
   AC_MSG_RESULT(no)
fi

# KRB5/GSSAPI configuration.
#
if test "x$with_gss" != xno  ; then
   AC_PATH_PROG(KRB5_CONFIG, krb5-config,no,[${PATH}:/usr/kerberos/bin:/usr/lib/mit/bin])
   if test "x$KRB5_CONFIG" != xno ; then 
      AC_DEFINE(HAVE_GSSAPI,1,[Defined when GSSAPI support is to be compiled.])
      BALSA_CFLAGS="$BALSA_CFLAGS `$KRB5_CONFIG --cflags gssapi`"
      BALSA_LIBS="$BALSA_LIBS `$KRB5_CONFIG --libs gssapi`"
      SAVE_CFLAGS="$CFLAGS"; CFLAGS="$BALSA_CFLAGS"
      AC_CHECK_HEADER([gssapi.h],
                      [AC_DEFINE([HAVE_HEIMDAL],1,[Defined for Heimdal.])],
        [AC_CHECK_HEADER([gssapi/gssapi.h],,
                         AC_MSG_ERROR([gssapi.h not found]))])
      CFLAGS="$SAVE_CFLAGS"
   else
      AC_MSG_ERROR([GSS requested but krb5-config not found. Install krb5-devel/heimdal-devel])
   fi
fi

# Libnotify configuration
if test x$with_libnotify != xno; then
    PKG_CHECK_MODULES(NOTIFY, [ libnotify ])
    AC_DEFINE(HAVE_NOTIFY,1,[Defined when libnotify can be used.])
    BALSA_LIBS="$BALSA_LIBS -lnotify"
    BALSA_AB_LIBS="$BALSA_AB_LIBS -lnotify"
fi

# NetworkManager
if test "$with_nm" != no ; then
  PKG_CHECK_EXISTS([libnm-glib], 
  [ PKG_CHECK_MODULES(LIBNM_GLIB, [ libnm-glib >= 0.7.0]) ],
  [ PKG_CHECK_MODULES(LIBNM_GLIB, [ libnm_glib >= 0.7.0]) ])

  AC_DEFINE(HAVE_LIBNM_GLIB,1,[ Defined if NetworkManager-glib-devel is found.])
  BALSA_CFLAGS="$BALSA_CFLAGS $LIBNM_GLIB_CFLAGS"
  LIBS="$LIBS $LIBNM_GLIB_LIBS"
else
AC_MSG_CHECKING([NetworkManager-glib-devel])
 AC_MSG_RESULT([no])
fi

# Compface configuration
#
AC_MSG_CHECKING([whether to build Compface support])
if test "x$with_compface" != xno  ; then
    AC_MSG_RESULT([yes])
    test "$with_compface" != yes && BALSA_LIBS="$BALSA_LIBS -L$with_compface/lib"
    AC_CHECK_LIB(compface, uncompface, BALSA_LIBS="$BALSA_LIBS -lcompface",
	AC_MSG_ERROR([*** You enabled Compface but the library is not found.]),)
    AC_DEFINE(HAVE_COMPFACE, 1, 
              [Defined when Compface support is requested.])
    test "$with_compface" != yes && CPPFLAGS="$CPPFLAGS -I$with_compface/include"
else
    AC_MSG_RESULT([no])
fi

# GtkSourceView configuration
#
if test x$with_gtksourceview != xno; then
    if test x$with_gtksourceview = x1 || ! $PKG_CONFIG --exists gtksourceview-2.0 ; then
	PKG_CHECK_MODULES(GTKSOURCEVIEW, [ gtksourceview-1.0 ])
    	AC_DEFINE(HAVE_GTKSOURCEVIEW,1,[Defined when GtkSourceView can be used.])
	AC_MSG_NOTICE([Note: using GtkSourceview Version 1])
    else
	PKG_CHECK_MODULES(GTKSOURCEVIEW, [ gtksourceview-2.0 ])
    	AC_DEFINE(HAVE_GTKSOURCEVIEW,2,[Defined when GtkSourceView can be used.])
	AC_MSG_NOTICE([Note: using GtkSourceview Version 2])
	with_gtksourceview=2
    fi
    BALSA_CFLAGS="$BALSA_CFLAGS $GTKSOURCEVIEW_CFLAGS"
    BALSA_LIBS="$BALSA_LIBS $GTKSOURCEVIEW_LIBS"
fi
AM_CONDITIONAL([BUILD_WITH_GTKSOURCEVIEW2], [test x$with_gtksourceview = x2 ])

# libUnique configuration
#
AC_MSG_CHECKING([whether to build with libUnique])
if test x$with_unique != xno; then
    AC_MSG_RESULT([yes])
    PKG_CHECK_MODULES(UNIQUE, [ unique-1.0 ])
    AC_DEFINE(HAVE_UNIQUE,1,[Defined when libUnique can be used.])
    BALSA_CFLAGS="$BALSA_CFLAGS $UNIQUE_CFLAGS"
    BALSA_LIBS="$BALSA_LIBS $UNIQUE_LIBS"
else
    AC_MSG_RESULT([no])
fi
AM_CONDITIONAL([BUILD_WITH_UNIQUE], [test $with_unique = "yes" ])

# libcanberra configuration
#
AC_MSG_CHECKING([whether to build with canberra])
if test x$with_canberra != xno; then
    AC_MSG_RESULT([yes])
    PKG_CHECK_MODULES(CANBERRA, [ libcanberra-gtk ])
    AC_DEFINE(HAVE_CANBERRA,1,[Defined when canberra can be used.])
    BALSA_CFLAGS="$BALSA_CFLAGS $CANBERRA_CFLAGS"
    BALSA_LIBS="$BALSA_LIBS $CANBERRA_LIBS"
else
    AC_MSG_RESULT([no])
fi

# ige-mac-integration (Mac OS X desktop only)
AC_MSG_CHECKING([whether to include Mac OS X Desktop Integration])
if test x$use_igemacint != xno; then
    AC_MSG_CHECKING([for ige-mac-integration >= 0.8])
    if $PKG_CONFIG --atleast-version=0.8 ige-mac-integration ; then
	AC_MSG_RESULT([yes])
	AC_DEFINE(HAVE_MACOSX_DESKTOP,1,[Defined when menus can be integrated into the Mac OS X desktop])
	BALSA_CFLAGS="$BALSA_CFLAGS `$PKG_CONFIG --cflags ige-mac-integration`"
	BALSA_LIBS="$BALSA_LIBS `$PKG_CONFIG --libs ige-mac-integration`"
	BALSA_AB_LIBS="$BALSA_AB_LIBS `$PKG_CONFIG --libs ige-mac-integration`"
    else
	AC_MSG_ERROR([no])
    fi
else
    AC_MSG_RESULT([no])
fi

# Touch UI configuration
#
AC_MSG_CHECKING([whether to configure for Touch UI])
if test x$enable_touch_ui != xno; then
    AC_DEFINE(ENABLE_TOUCH_UI,1,[Defined when touchscreen optimized UI is to be enabled.])
    AC_MSG_RESULT([yes])
else
    AC_MSG_RESULT([no])
fi

dnl #####################################################################
dnl 5. Headers.
dnl #####################################################################

# Spell check detection.
#
if test x$with_gtkspell != xno; then
    PKG_CHECK_MODULES(SPELL, [ gtkspell-2.0 enchant ])
    AC_DEFINE(HAVE_GTKSPELL,1,[Defined when GtkSpell can be used.])
    BALSA_CFLAGS="$BALSA_CFLAGS $SPELL_CFLAGS"
    BALSA_LIBS="$BALSA_LIBS $SPELL_LIBS"
else
    AC_CHECK_LIB(aspell, new_aspell_speller,,[
        AC_CHECK_LIB(pspell, new_pspell_manager,,[
            AC_MSG_ERROR([*** Neither aspell >=0.50 (recommended) nor pspell (deprecated) library was found. Please check http://balsa.gnome.org for information on the required files. Alternatively, check config.log for clues.])
        ], -lstdc++ -lpspell-modules)
    ], -lstdc++)
    AC_CHECK_HEADER(pspell/pspell.h,,
                    AC_MSG_ERROR([** aspell/pspell library found but not related header files. This probably means corresponding aspell-devel package is not installed ]))
fi
AM_CONDITIONAL([BUILD_WITH_GTKSPELL], [test $with_gtkspell = "yes"])

dnl #####################################################################
dnl 6. Typedefs, structures and compiler characteristics.
dnl #####################################################################
AC_C_CONST
AC_STDC_HEADERS

AC_DEFINE([_XOPEN_SOURCE],[500],[We strive for XOPEN compliance])
AC_CHECK_DECLS([localtime_r, gmtime_r, ctime_r], [], [], [[#include <time.h>]])
AC_CHECK_FUNCS([localtime_r gmtime_r ctime_r])
AC_CHECK_HEADER([zlib.h],,AC_MSG_ERROR([zlib library required]))

# more warnings.
#

if test "$GCC" = "yes" -a "$set_more_warnings" != "no"; then
	for option in -Wsign-compare -Wpointer-arith -Wnested-externs \
            -Wchar-subscripts -Wuninitialized -Wmissing-prototypes    \
            -Wreturn-type -Wdeclaration-after-statement ; do
                SAVE_CFLAGS="$CFLAGS"
                CFLAGS="$CFLAGS $option"
                AC_MSG_CHECKING([whether gcc understands $option])
                AC_TRY_COMPILE([], [],
                        has_option=yes,
                        has_option=no,)
                if test $has_option = no; then
                  CFLAGS="$SAVE_CFLAGS"
                fi
                AC_MSG_RESULT($has_option)
                unset has_option
                unset SAVE_CFLAGS
        done
        unset option
fi

# extra warnings for GIT.
#

if test -d "${srcdir}/.git" ; then
	BALSA_FROM_GIT=yes
	BALSA_CFLAGS="$BALSA_CFLAGS -Wall -Werror"
        if $PKG_CONFIG --atleast-version=2.18 gtk+-2.0; then
            BALSA_DEFS="-DGSEAL_ENABLE"
        fi
        BALSA_DEFS="$BALSA_DEFS \
-DG_DISABLE_DEPRECATED \
-DGDK_DISABLE_DEPRECATED \
-DGDK_PIXBUF_DISABLE_DEPRECATED \
-DGMIME_DISABLE_DEPRECATED \
-DGNOME_DISABLE_DEPRECATED \
-DGNOME_DISABLE_DEPRECATED_SOURCE \
-DGNOME_VFS_DISABLE_DEPRECATED \
-DGTK_DISABLE_DEPRECATED \
-DNM_DISABLE_DEPRECATED \
-DPANGO_DISABLE_DEPRECATED \
-D_FORTIFY_SOURCE=2 \
"
else
	BALSA_FROM_GIT=no
fi

AC_MSG_CHECKING([for GIT information])
AC_MSG_RESULT($BALSA_FROM_GIT)

dnl #####################################################################
dnl 7. extra functions.
dnl #####################################################################
AC_CHECK_FUNCS(getaddrinfo getnameinfo gai_strerror)

# isblank()
AC_MSG_CHECKING([for isblank])
AC_TRY_LINK( [#include <ctype.h>], [return isblank('a');],
	     AC_MSG_RESULT(yes),
	     [AC_MSG_RESULT(no)
	      AC_DEFINE(isblank,isspace,
                        [Redefined to isspace if isblank is not available.])] )


dnl ###########################################################################
dnl Remember our prefixes
dnl   The way prefix et al are defined makes us jump through some hoops.
dnl ###########################################################################

if test "x$prefix" = "xNONE"; then
	prefix=$ac_default_prefix
fi
dnl datadir='${prefix}/share', so we must eval it or something. This works
BALSA_STD_PREFIX=`eval "echo ${prefix}"`
BALSA_DATA_PREFIX=`eval "echo ${datadir}/balsa"`
dnl autoconf-2.60 leaves a literal "${prefix}" in BALSA_DATA_PREFIX, so
dnl we clean it once more.
BALSA_DATA_PREFIX=`eval "echo ${BALSA_DATA_PREFIX}"`
AC_SUBST(BALSA_DATA_PREFIX)

dnl Consolidate them!
AC_PROG_AWK
BALSA_COMMON_PREFIXES=`sort<<EOF|uniq|${AWK} '{if(l!~/^$/)print l ",";l=$0}END{print l}'
"${BALSA_STD_PREFIX}"
"${BALSA_DATA_PREFIX}"
EOF
`

dnl Better way to do this?
AC_DEFINE_UNQUOTED( BALSA_STD_PREFIX, "$BALSA_STD_PREFIX",
                   [Directory for all balsa files.] )
AC_DEFINE_UNQUOTED( BALSA_DATA_PREFIX, "$BALSA_DATA_PREFIX",
	           [Directory for storing balsa data files.] )
AC_DEFINE_UNQUOTED( BALSA_COMMON_PREFIXES, `echo $BALSA_COMMON_PREFIXES`,
	           [Common directories for storing balsa icons.] ) 
dnl bleah


dnl ###########################################################################
dnl determine the base folder for installing the extra icons
dnl ###########################################################################

AC_ARG_ENABLE(extra-mimeicons,
        AC_HELP_STRING([--enable-extra-mimeicons],
                       [Install extra MIME icons (needs write access to the gtk+ icon folder, default=yes)]),[
	install_mimeicons=$enableval
],[
	install_mimeicons=yes
])
AM_CONDITIONAL([INSTALL_MIME_ICONS], [test $install_mimeicons = "yes"])
HICOLOR_ICON_DIR=`$PKG_CONFIG --variable=prefix gtk+-2.0`/share/icons/hicolor
AC_SUBST(HICOLOR_ICON_DIR)
dnl however, we do not mess with the icon caches - it messes up more
dnl things that it fixes, for eg. user installs, package builds etc.
dnl AC_PATH_PROG(GTK_UPDATE_ICON_CACHE, gtk-update-icon-cache)

dnl #####################################################################
dnl 8. Output
dnl #####################################################################

AC_OUTPUT([
Makefile
balsa.1
balsa.spec
po/Makefile.in
sounds/Makefile
images/Makefile
images/mimetypes/Makefile
images/16x16/Makefile
images/24x24/Makefile
doc/Makefile
libbalsa/Makefile
libbalsa/imap/Makefile
libinit_balsa/Makefile
src/Makefile
])

dnl ##########################################################################
dnl Summary
dnl ##########################################################################

echo ""
echo "================ Final configuration ==================="
echo "    Installing into prefix: $prefix"
echo "      Using multithreading: $use_threads"
echo "   Enable compile warnings: $set_more_warnings"
echo "                Use ESMTP : $with_esmtp"
echo "               HTML widget: $use_html_widget"
echo " Use Unique (experimental): $with_unique"
echo "                 Use GNOME: $with_gnome"
echo "              Use Canberra: $with_canberra"
echo "                 Use GPGME: $gpgmecfg"
echo "                  Use LDAP: $with_ldap"
echo "                   Use GSS: $with_gss"
echo "                Use SQLite: $with_sqlite"
echo "                   Use SSL: $with_ssl"
echo "        Use NetworkManager: $with_nm"
echo "              Use GtkSpell: $with_gtkspell"
echo "             Use Libnotify: $with_libnotify"
echo "         Use GtkSourceView: $with_gtksourceview"
echo "              Use Compface: $with_compface"
echo "  Install extra MIME icons: $install_mimeicons"
echo "    Configure for Touch UI: $enable_touch_ui"
dnl echo "                  Use SASL: $need_sasl"
echo ""

if test x"$BALSA_FROM_GIT" = xyes ; then
	echo "============================== NOTICE ================================"
	echo "                You are using Balsa from GIT source."
	echo "  The program is likely to be unstable, contain incomplete features,"
	echo "or just plain not work. Use it at your own risk. You have been warned."
	echo "======================================================================"
	echo ""
fi
